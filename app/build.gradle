/*
 * Copyright {2017} {Aashrey Kamal Sharma}
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply from: "../artifacts.gradle"

buildscript {
    repositories { maven { url 'https://maven.fabric.io/public' } }
    dependencies {
        // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath 'io.fabric.tools:gradle:1.21.6'
    }
}

// Load walls properties
def wallsPropertiesFile = rootProject.file("sublime.properties");
def wallsProperties = new Properties()
wallsProperties.load(new FileInputStream(wallsPropertiesFile))

int versionCodeInt = 15;
String versionNameString = "1.6.1";

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.aashreys.walls"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode versionCodeInt
        versionName versionNameString
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            storeFile file(wallsProperties['storeFile'])
            storePassword wallsProperties['storePassword']
            keyAlias wallsProperties['keyAlias']
            keyPassword wallsProperties['keyPassword']
        }
    }

    String STRING = "String";
    String STRING_RESOURCE = "string"
    String UNSPLASH_API_KEY = "UNSPLASH_API_KEY"
    String GOOGL_API_KEY = "GOOGL_API_KEY";
    String FLICKR_API_KEY = "FLICKR_API_KEY";
    String BOOLEAN = "boolean";
    String IS_PRODUCTION = "IS_PRODUCTION";
    String VERSION_NAME = "version_name"

    buildTypes {
        buildTypes.each {
            // Common build config vars
            it.buildConfigField(STRING, UNSPLASH_API_KEY, wallsProperties['unsplashApiKey'])
            it.buildConfigField(STRING, GOOGL_API_KEY, wallsProperties['googlApiKey'])
            it.buildConfigField(STRING, FLICKR_API_KEY, wallsProperties['flickrApiKey'])
            it.resValue(STRING_RESOURCE, VERSION_NAME, versionNameString)
        }
        release {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled true
            zipAlignEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.release'
            buildConfigField BOOLEAN, IS_PRODUCTION, "true"
        }
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix '.debug'
            ext.enableCrashlytics = false
            buildConfigField BOOLEAN, IS_PRODUCTION, "false"
        }
    }
}

repositories {
    maven { url "https://maven.google.com" }
    maven { url 'https://maven.fabric.io/public' }
    flatDir { dirs 'libs' }
}

dependencies {
    // General
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "org.apache.commons:commons-lang3:3.5"

    // Support Libraries
    compile "com.android.support:appcompat-v7:${supportLibraryVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibraryVersion}"
    compile "com.android.support:support-core-ui:${supportLibraryVersion}"
    compile "com.android.support:support-fragment:${supportLibraryVersion}"
    compile "com.android.support:design:${supportLibraryVersion}"
    compile "com.android.support:customtabs:${supportLibraryVersion}"
    compile "com.android.support:support-vector-drawable:${supportLibraryVersion}"

    // Crash reporting
    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true;
    }

    // Custom views
    compile "com.wefika:flowlayout:0.4.1"

    // Persistence
    compile 'io.paperdb:paperdb:2.1'

    // Networking
    compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile 'com.google.code.gson:gson:2.8.1'
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    compile "com.github.bumptech.glide:glide:${glideVersion}"
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'

    // Dependency Injection
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    compile "com.google.dagger:dagger:${daggerVersion}"
    provided 'com.google.auto.factory:auto-factory:1.0-beta3'
    // Required for the @Generated annotation by Google Auto Factory
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // Explicitly defining dependencies to resolve version conflicts in some of the above libraries
    compile 'com.squareup:javawriter:2.5.1'
    compile "org.objenesis:objenesis:2.5"

    // ReactiveX
    compile 'io.reactivex.rxjava2:rxjava:2.1.2'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile "com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}"

    // Unit Testing
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.5'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    // Since the JSON library used by Android is a part of the SDK we can't mock it easily, so we're
    // adding a pure Java JSON library as a test dependency instead of mocking it in every test.
    testCompile 'org.json:json:20160810'
    testCompile "com.squareup.okhttp3:mockwebserver:${okHttpVersion}"

    // Instrumentation Testing
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.squareup', module: 'javawriter'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
}
